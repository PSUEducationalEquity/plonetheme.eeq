<?xml version="1.0" encoding="utf-8"?>
<rules xmlns="http://namespaces.plone.org/diazo"
       xmlns:css="http://namespaces.plone.org/diazo/css"
       xmlns:xhtml="http://www.w3.org/1999/xhtml"
       xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
       xmlns:xi="http://www.w3.org/2001/XInclude">

  <xsl:param name="portal_url"></xsl:param>
  <theme href="index.html" />
  <notheme css:if-not-content="#visual-portal-wrapper" />

  <rules css:if-content="#portal-top">
    <!-- Attributes -->
    <copy attributes="*" css:theme="html" css:content="html" />
    <!-- Base tag -->
    <before css:theme="title" css:content="base" />
    <!-- Title -->
    <replace css:theme="title" css:content="title" />
    <!-- Pull in Plone Meta -->
    <after css:theme-children="head" css:content="head meta" />
    <!-- Don't use Plone icons, use the theme's -->
    <drop css:content="head link[rel='apple-touch-icon']" />
    <drop css:content="head link[rel='shortcut icon']" />
    <drop css:theme=".theme-drop" />
    <!-- CSS -->
    <after css:theme-children="head" css:content="head link" />
    <after css:theme-children="head" css:content="head style" />
    <!-- Script -->
    <after css:theme-children="head" css:content="head script" />
  </rules>

  <!-- Copy over the id/class attributes on the body tag. This is important for per-section styling -->
  <copy attributes="*" css:content="body" css:theme="body" />

  <!-- move global nav -->
  <replace css:theme-children="#mainnavigation" css:content-children="#portal-mainnavigation" method="raw" />
  <replace css:theme-children="#navbarGlobal">
      <ul class="navbar-nav">
        <xsl:for-each css:select="#portal-globalnav li a">
          <li class="nav-item">
            <a class="nav-link"><xsl:attribute name='href'><xsl:value-of select='@href'/></xsl:attribute>
              <xsl:copy-of select="./text()" />
            </a>
          </li>
        </xsl:for-each>
      </ul>
  </replace>

  <!-- Office navigation -->
  <replace css:theme-children="#navbar-office-left" css:content-children="#office-navbar-left" />
  <replace css:theme-children="#navbar-office-right" css:content-children="#office-navbar-right" />

  <!-- Office header -->
  <replace css:theme-children=".office-name-wrapper .office-name" css:content-children="#office-title" />
  <replace css:theme="#hero-image" css:content="#hero-image" />

  <!-- full-width breadcrumb -->
  <replace css:theme-children="nav#portal-breadcrumbs">
    <ol class="breadcrumb">
      <xsl:for-each select="//*[@id='portal-breadcrumbs']//li">
        <li class="breadcrumb-item">
          <xsl:copy-of select="./*" />
        </li>
      </xsl:for-each>
    </ol>
  </replace>
  
  <!-- Central column -->
  <replace css:theme="#content-container" method="raw">

    <xsl:variable name="central">
      <xsl:if test="//aside[@id='portal-column-one'] and //aside[@id='portal-column-two']">col-xs-12 col-sm-12 col-md-6</xsl:if>
      <xsl:if test="//aside[@id='portal-column-two'] and not(//aside[@id='portal-column-one'])">col-xs-12 col-sm-12 col-md-9</xsl:if>
      <xsl:if test="//aside[@id='portal-column-one'] and not(//aside[@id='portal-column-two'])">col-xs-12 col-sm-12 col-md-9</xsl:if>
      <xsl:if test="not(//aside[@id='portal-column-one']) and not(//aside[@id='portal-column-two'])">col-xs-12 col-sm-12 col-md-12</xsl:if>
    </xsl:variable>

    <div class="{$central}">
      <div class="row">
        <div class="col-xs-12 col-sm-12">
          <xsl:apply-templates css:select="#content" />
        </div>
      </div>
      <footer class="row">
        <div class="col-xs-12 col-sm-12">
          <xsl:copy-of css:select="#viewlet-below-content" />
        </div>
      </footer>
    </div>
  </replace>

  <!-- Alert message -->
  <replace css:theme-children="#global_statusmessage" css:content-children="#global_statusmessage" />

  <!-- Left column -->
  <rules css:if-content="#portal-column-one">
    <replace css:theme="#column1-container">
      <xsl:variable name="columnone">
        <xsl:if test="//aside[@id='portal-column-two']">col-xs-12 col-sm-6 col-md-3</xsl:if>
        <xsl:if test="//aside[@id='portal-column-one'] and not(//aside[@id='portal-column-two'])">col-xs-12 col-sm-12 col-md-3</xsl:if>
      </xsl:variable>
      <div class="{$columnone} sidebar-offcanvas">
        <aside id="portal-column-one">
          <xsl:copy-of css:select="#portal-column-one > *" />
        </aside>
      </div>
    </replace>
  </rules>
  <rules css:if-not-content="#portal-column-one">
    <replace css:theme="#column1-container">
    </replace>
  </rules>

  <!-- Right column -->
  <rules css:if-content="#portal-column-two">
    <replace css:theme="#column2-container">
      <xsl:variable name="columntwo">
        <xsl:if test="//aside[@id='portal-column-one']">col-xs-12 col-sm-6 col-md-3</xsl:if>
        <xsl:if test="//aside[@id='portal-column-two'] and not(//aside[@id='portal-column-one'])">col-xs-12 col-sm-12 col-md-3</xsl:if>
      </xsl:variable>
      <div class="{$columntwo} sidebar-offcanvas" role="complementary">
        <aside id="portal-column-two">
          <xsl:copy-of css:select="#portal-column-two > *" />
        </aside>
      </div>
    </replace>
  </rules>
  <rules css:if-not-content="#portal-column-two">
    <replace css:theme="#column2-container">
    </replace>
  </rules>

  <!-- Content header -->
  <replace css:theme="#portal-top" css:content-children="#portal-top" />

  <!-- Footer -->
  <replace css:theme-children="#footer-portlet-row">
    <div class="col-lg-4">
      <h3 class="text-white">Educational Equity</h3>
      <ul class="list-unstyled">
        <xsl:for-each css:select="footer#portal-footer-wrapper .portlet-collection-educational-equity li">
          <xsl:copy-of select="." />
        </xsl:for-each>
      </ul>
      <h3 class="text-white">Sponsored Events</h3>
      <ul class="list-unstyled">
        <xsl:for-each css:select="footer#portal-footer-wrapper .portlet-collection-sponsored-events li">
          <xsl:copy-of select="." />
        </xsl:for-each>
      </ul>
    </div>
    <div class="col-lg-4">
      <h3 class="text-white">Penn State Resources</h3>
      <ul class="list-unstyled">
        <xsl:for-each css:select="footer#portal-footer-wrapper .portlet-collection-penn-state-resources li">
          <xsl:copy-of select="." />
        </xsl:for-each>
      </ul>
    </div>
    <div class="col-lg-4">
      <h3 class="text-white">For Faculty</h3>
      <ul class="list-unstyled">
        <xsl:for-each css:select="footer#portal-footer-wrapper .portlet-collection-for-faculty li">
          <xsl:copy-of select="." />
        </xsl:for-each>
      </ul>
      <h3 class="text-white">For Staff</h3>
      <ul class="list-unstyled">
        <xsl:for-each css:select="footer#portal-footer-wrapper .portlet-collection-for-staff li">
          <xsl:copy-of select="." />
        </xsl:for-each>
      </ul>
    </div>
  </replace>

  <!-- Add additional footer content, like script tags (from site settings). -->
  <after
      css:theme-children="html body"
      content="//footer[@id='portal-footer-wrapper']/*[not(@class='row')]"
      />
  <!-- Top banner viewlet -->
  <replace css:theme="#above-header-banner" css:content-children="#AboveHeaderBanner" />

  <!-- toolbar -->
  <replace css:theme="#portal-toolbar" css:content="#edit-bar" css:if-not-content=".ajax_load" css:if-content=".userrole-authenticated" />
  <drop css:theme="#portal-toolbar" css:if-content=".userrole-anonymous" />
  <replace css:theme="#anonymous-actions" css:content-children="#portal-personaltools-wrapper" css:if-not-content=".ajax_load" css:if-content=".userrole-anonymous" />

  <!-- Link normalization -->
  <before css:theme-children="a.navbar-brand"><xsl:attribute name="href"><xsl:value-of select="$portal_url" />/</xsl:attribute></before>
  <before css:theme-children="a.report-bias"><xsl:attribute name="href"><xsl:value-of select="$portal_url" />/report-bias</xsl:attribute></before>
  <before css:theme-children="a.give-now"><xsl:attribute name="href"><xsl:value-of select="$portal_url" />/giving</xsl:attribute></before>
  <before css:theme-children="a.contact-button"><xsl:attribute name="href"><xsl:value-of select="$portal_url" />/contact-info</xsl:attribute></before>
  <before css:theme-children="a.site-map"><xsl:attribute name="href"><xsl:value-of select="$portal_url" />/sitemap</xsl:attribute></before>
  <before css:theme-children="a.accessibility"><xsl:attribute name="href"><xsl:value-of select="$portal_url" />/accessibility-info</xsl:attribute></before>
  <before css:theme-children="#searchGadget_form"><xsl:attribute name="action"><xsl:value-of select="$portal_url" />/@@search</xsl:attribute></before>

  <!-- Login button -->
  <before css:if-content="#portal-anontools" css:theme-children="#personaltools-login"><xsl:attribute name="href"><xsl:value-of select="$portal_url" />/login</xsl:attribute></before>
  <before css:if-content="#portal-anontools" css:theme-children="#personaltools-login"><xsl:attribute name="class">btn btn-sm btn-secondary pat-plone-modal</xsl:attribute></before>
  <before css:if-not-content="#portal-anontools" css:theme-children="#personaltools-login"><xsl:attribute name="href"><xsl:value-of select="$portal_url" />/logout</xsl:attribute></before>
  <replace css:if-not-content="#portal-anontools" css:theme-children="#personaltools-login">Log out</replace>

  <!-- Search button in header -->
  <replace css:theme="#searchGadget" css:content="#searchGadget" />
</rules>
